/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2006-2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.kordamp-parentpom' version '1.7.0'
    id 'org.kordamp.gradle.source-xref'       version '0.27.0'
    // id 'org.kordamp.gradle.clirr'             version '0.27.0'
} 

config {
    info {
        name          = 'Json-lib'
        description   = 'Java library for transforming beans, maps, collections, arrays and XML to JSON.'
        inceptionYear = '2006'
        tags          = ['json']

        specification { enabled = false }

        people {
            person {
                name  = 'Douglas Crockford'
                email = 'json@json.org'
                roles = ['Original source code developer']
            }
            person {
                name = 'Matt Small'
                organization {
                    name = 'WaveMaker'
                }
                roles = ['contributor']
            }
            person {
                name = 'Dan Fabulich'
                roles = ['contributor']
            }
            person {
                name = 'Masato Nagai'
                roles = ['contributor']
            }
            person {
                name = 'Gino Miceli'
                roles = ['contributor']
            }
            person {
                name = 'Mino Tonga'
                roles = ['contributor']
            }
            person {
                name = 'Yee_Keat Phuah'
                roles = ['contributor']
            }
            person {
                name = 'Oliver Zyngier'
                roles = ['contributor']
            }
            person {
                name = 'Marcelo Liberato'
                roles = ['contributor']
            }
            person {
                name = 'Mathias Herberst'
                roles = ['contributor']
            }
            person {
                name = 'Ales Novy'
                roles = ['contributor']
            }
            person {
                name = 'Peter Schubert'
                roles = ['contributor']
            }
            person {
                name = 'Ian Wolfcat Atha'
                roles = ['contributor']
            }
            person {
                name = 'Mischa Koelliker'
                roles = ['contributor']
            }
            person {
                name = 'Michel Racic'
                roles = ['contributor']
            }
        }

        credentials {
            sonatype {
                username = project.sonatypeUsername
                password = project.sonatypePassword
            }
        }

        scm {
            url                 = 'https://github.com/aalmiray/json-lib'
            connection          = 'scm:git:https://github.com/aalmiray/json-lib.git'
            developerConnection = 'scm:git:git@github.com:aalmiray/json-lib.git'
        }
    }

    sourceXref {
        inputEncoding = 'UTF-8'
    }
	
	// clirr {
	//     baseline = [project.group, project.name, project.previousVersion].join(':')
	// }
}

allprojects {
    apply plugin: 'base'
    apply plugin: 'idea'

    repositories {
        jcenter()
    }

    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}

idea {
    project {
        jdkName '1.7'
        languageLevel '1.7'

        ipr {
            withXml { provider ->
                def node = provider.asNode()
                node.component.find { it.'@name' == 'VcsDirectoryMappings' }?.mapping[0].'@vcs' = 'Git'

                def compilerConfiguration = node.component.find { it.'@name' == 'CompilerConfiguration' }
                compilerConfiguration.remove(compilerConfiguration.annotationProcessing)
                compilerConfiguration.append(new XmlParser().parseText("""
    <annotationProcessing>
      <profile default="true" name="Default" enabled="true">
        <processorPath useClasspath="true" />
      </profile>
    </annotationProcessing>"""))
            }
        }
    }
}