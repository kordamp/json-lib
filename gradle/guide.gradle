/*
 * Copyright 2006-2015 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * This product includes software developed by Douglas Crockford
 * (http://www.crockford.com) and released under the Apache Software
 * License version 2.0 in 2006.
 */

apply plugin: 'org.asciidoctor.convert'
apply plugin: 'org.ajoberstar.github-pages'

javadoc {
    excludes = ['**/*.html', 'META-INF/**']

    options.use         = true
    options.splitIndex  = true
    options.encoding    = 'UTF-8'
    options.author      = true
    options.version     = true
    options.windowTitle = "${project.name} ${project.version} API"
    options.docTitle    = "${project.name} ${project.version} API"
    options.footer      = javadocFooter
    options.links       = ['http://www.slf4j.org/apidocs/',
                           'http://junit.org/javadoc/latest/',
                           'http://docs.oracle.com/javase/7/docs/api/']
}

task jarApi(type: Jar, dependsOn: javadoc) {
    archiveName    = "${project.name}-${project.version}-javadoc.jar"
    destinationDir = "$buildDir/assemble/jars" as File
    from "$buildDir/api"
}

asciidoctor {
    baseDir   = project.file('guide/src/asciidoc')
    sourceDir = project.file('guide/src/asciidoc')

    attributes = [
        toc                     : 'left',
        doctype                 : 'book',
        icons                   : 'font',
        encoding                : 'utf-8',
        sectlink                : true,
        sectanchors             : true,
        numbered                : true,
        linkattrs               : true,
        imagesdir               : 'images',
        linkcss                 : true,
        'source-highlighter'    : 'coderay',
        'coderay-linenums-mode' : 'inline',
        'project-author'        : 'Andres Almiray',
        'project-url'           : project.project_url,
        'project-vcs'           : project.project_scm,
        'project-issue-tracker' : project.project_issues,
    ]

    sources {
        include 'index.adoc'
    }

}

task guide(type: Copy, dependsOn: [javadoc, asciidoctor]) {
    destinationDir = "$buildDir/guide" as File
    from(javadoc.outputs) { into 'api' }
    from("$buildDir/asciidoc")
}

task guideZip(type: Zip, dependsOn: guide) {
    baseName = "${project.name}-guide"
    from "$buildDir/guide"
}

if (!project.hasProperty('githubUsername')) ext.githubUsername = ''
if (!project.hasProperty('githubPassword')) ext.githubPassword = ''

githubPages {
    repoUri = project.project_scm
    pages {
        from guide.outputs.files
    }

    credentials {
        username = githubUsername
        password = githubPassword
    }
}

publishGhPages.dependsOn(guide)
